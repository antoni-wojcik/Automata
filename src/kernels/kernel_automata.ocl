__constant sampler_t sampler = CLK_NORMALIZED_COORDS_FALSE | CLK_ADDRESS_NONE | CLK_FILTER_NEAREST;

#define COLOR_MAX 255
#define COLOR_MID 128

kernel void processTexture(__read_only image2d_t image_in, __write_only image2d_t image_out) {
    int x = get_global_id(0);
    int y = get_global_id(1);
    
    float4 pixel_data_f = read_imagef(image_in, sampler, (int2)(x, y));
    
    uint4 pixel_data_u = (uint4)(0, 0, 0, 1);
    
    if(pixel_data_f.x > 0.0f) pixel_data_u.x = COLOR_MAX;
    if(pixel_data_f.y > 0.0f) pixel_data_u.y = COLOR_MAX;
    if(pixel_data_f.z > 0.0f) pixel_data_u.z = COLOR_MAX;
    
    write_imageui(image_out, (int2)(x, y), pixel_data_u);
}

kernel void iterate(__read_only image2d_t image_in, __write_only image2d_t image_out) {

    int x = get_global_id(0);
    int y = get_global_id(1);
    
    int width = get_image_width(image_in);
    int height = get_image_height(image_in);
    
    int counter = 0;
    
    bool alive = false;
    if(read_imageui(image_in, sampler, (int2)(x, y)).x > 0) alive = true;
    
    for(int i = -1; i < 2; i++) for(int j = -1; j < 2; j++) {
        if(i == 0 && j == 0) continue;
        
        uint pixel_data = read_imageui(image_in, sampler, (int2)((x + i + width) % width, (y + j + height) % height)).x;
        
        if(pixel_data > 0) counter++;
    }
    
    uint col = 0;
    
    if(alive) {
        if(2 <= counter && counter <= 3) col = COLOR_MAX;
    } else {
        if(3 <= counter && counter <= 3) col = COLOR_MID;
    }
    
    write_imageui(image_out, (int2)(x, y), (uint4)(col, col, col, 1));
}
